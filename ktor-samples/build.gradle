def host;

def hostServerProp = project.hasProperty('hostServer') ? project.hostServer : 'netty'
def enableAlpnProp = project.hasProperty('enableAlpn')

switch (hostServerProp) {
    case 'jetty': host = 'jetty'; break
    case 'netty': host = 'netty'; break
    case 'tomcat': host = 'tomcat'; break
    default: throw new InvalidUserDataException("Invalid host ${project.hostServer}")
}


subprojects {
    apply plugin: 'application'

    dependencies {
        compile project(":ktor-server:ktor-server-core")
        compile "org.slf4j:jul-to-slf4j:1.7.12"

        compile project(":ktor-server:ktor-server-$host")

        if (enableAlpnProp) {
            if (hostServerProp == 'netty') {
                def nativeClassifier;
                def osName = System.getProperty('os.name').toLowerCase()

                if (osName.contains('win')) {
                    nativeClassifier = 'windows-x86_64'
                } else if (osName.contains('linux')) {
                    nativeClassifier = 'linux-x86_64'
                } else if (osName.contains('mac')) {
                    nativeClassifier = 'osx-x86_64'
                } else {
                    throw new InvalidUserDataException("Unsupoprted os family $osName")
                }

                compile group: 'io.netty', name: 'netty-tcnative', version: netty_tcnative_version
                compile group: 'io.netty', name: 'netty-tcnative-boringssl-static', version: netty_tcnative_version
                compile group: 'io.netty', name: 'netty-tcnative-boringssl-static', version: netty_tcnative_version, classifier: nativeClassifier
            } else if (hostServerProp == 'jetty') {
                boot group: 'org.mortbay.jetty.alpn', name: 'alpn-boot', version: jetty_alpn_boot_version
            }
        }
    }

    mainClassName = "io.ktor.server.${host}.DevelopmentHost"

    afterEvaluate {
        def bootClasspath = configurations.boot.files
        def runTask = getTasksByName("run", false).first() as JavaExec

        runTask.jvmArgs(bootClasspath.collect { "-Xbootclasspath/p:${it.absolutePath}" })
    }
}
